ACTOR GenericCasing
{
	Projectile
	-NOGRAVITY
	-NOBLOCKMAP
	+BOUNCEONACTORS
	BounceType Doom
	BounceSound "bulletcasing/bounce"
	BounceFactor 0.5
	WallBounceFactor 0.2
	Mass 5
	Radius 3
	Height 3
}

ACTOR PistolCasing : GenericCasing
{
	Scale 0.075
	States
	{
	Spawn:
		CAS1 ABCDEFGH 2 A_SpawnItemEx("CasingSmoke")
		Loop
	Death:
		"####" A 0 A_Jump(256,1,2,3)
		"####" ACG 0 A_Jump(256,"Death1")
	Death1:
		"####" "#" 1000
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR RifleCasing : PistolCasing
{
	Scale 0.15
	States
	{
	Spawn:
		CAS4 ABCDEFGH 2 A_SpawnItemEx("CasingSmoke")
		Loop
	}
}

ACTOR MagnumCasing : PistolCasing
{
	Scale 0.125
	States
	{
	Spawn:
		CAS3 ABCDEFGH 2 A_SpawnItemEx("CasingSmoke")
		Loop
	}
}

ACTOR ShotgunCasing : PistolCasing
{
	WallBounceSound "shellcasing/bounce"
	BounceSound "shellcasing/bounce"
	DeathSound "shellcasing/stop"
	Scale 0.175
	States
	{
	Spawn:
		CAS2 ABCDEFGH 2 A_SpawnItemEx("CasingSmoke")
		Loop
	}
}

// And putting this here because I can.

ACTOR GunSmokeSpawner
{
    Radius 1
    Height 1
    Speed 25
    Projectile
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
    Death:
        TNT1 A 0 A_CustomMissile("GunSmoke",-2,0,0)
        Stop
    }
}

ACTOR GunSmoke
{
	Radius 1
	Height 1
	RenderStyle Add
	Scale .025
	+NOGRAVITY
	+NOBLOCKMAP
	+FLOORCLIP
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 A_SetScale((scalex + 0.025), (scaley + 0.025))
		Stop
	}
}

ACTOR CasingSmoke : GunSmoke
{
	Scale .025
	Alpha 0.4
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 A_SetScale((scalex + 0.005), (scaley + 0.005))
		Stop
	}
}

ACTOR CacoSmoke : GunSmoke
{
	Scale 0.5
}