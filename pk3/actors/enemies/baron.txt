
// The Bruiser Brothers

ACTOR HZBaronOfHell replaces BaronOfHell
{
	Health 1000
	Radius 24
	Height 64
	Mass 1000
	Speed 2.66666667
	PainChance 50
	Monster
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	Obituary "%o was brutalized by the Baron of Hell."
	HitObituary "%o was severed by the Baron of Hell."
	ActiveSound "baron/sight"
	BloodType GreenSquirt
	BloodColor "58 73 22"
	States
	{
	Spawn:
		NBRN AB 10 A_Look
		Loop
	See:
		NBRN A 1 A_Chase
		NBRN AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN A 1 A_Chase
		NBRN AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN B 1 A_Chase
		NBRN BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN B 1 A_Chase
		NBRN BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN C 1 A_Chase
		NBRN CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN C 1 A_Chase
		NBRN CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN D 1 A_Chase
		NBRN DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NBRN D 1 A_Chase
		NBRN DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Goto See
	Melee:
		NBRN E 0 A_PlaySound("gauntimp/meleeswing")
	Missile:
		NBRN EF 8 A_FaceTarget
		NBRN G 8 A_CustomComboAttack("BaronBall2",32,10 * random(1,8),"Weapons/Sledge/Hit")
		Goto See
	Pain:
		NBRN H 2
		NBRN H 2 A_Pain
		Goto See
	Death:
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O 0 A_BossDeath
		BOSS O -1
		Stop
	/*Death:
		BOSS I 8 A_SpawnItemEx("GreenSquirt",0,0,32)
		TNT1 A 0 A_SpawnItemEx("GreenSquirt",0,0,32)
		BOSS J 8 A_Scream
		BOSS K 8 A_SpawnItemEx("GreenSquirt",0,0,32)
		TNT1 A 0 A_SpawnItemEx("GreenSquirt",0,0,32)
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O 0 A_BossDeath
		BOSS O 1 A_JumpIf(velx + vely + velz == 0,1)
		Wait
		BOSS O -1 A_SpawnItemEx("GreenPool",0,0,8)
		Stop*/
	Raise:
		BOSS ONMLKJI 8
		Goto See
	}
}

ACTOR BaronBall2 : BaronBall
{
 	Damage 10
	Speed 10
	Scale .95
	DeathSound "gauntimp/voidexplod"
	States
	{
	Spawn:
		BAL7 A 0 A_PlaySound("gauntimp/voidloop",CHAN_BODY,1,1)
		ARCB AAABBBCCC 1 BRIGHT A_SpawnItemEx("BaronBallEffect")
		loop
	Death:
		ARCB JKLMN 3 bright
		Stop
	}
}

ACTOR BaronBallEffect
{
	RenderStyle Add
	Scale 0.5
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		ARCB DEFGHI 1
		Stop
	}
}

ACTOR KnightBall : BaronBall2
{
	Damage 6
	Speed 18
	Scale .65
}
