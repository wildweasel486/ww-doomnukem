ACTOR DNRifleSeq : Inventory{}

ACTOR DNRifle : DNWeapon replaces Chaingun
{
	Weapon.AmmoType "DNRifleAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "DNRifleAmmoPickup"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 2
	Weapon.SlotNumber 4
	Inventory.PickupSound "Weapons/Rifle/Click"
	Inventory.PickupMessage "You got the K&S R90-PCW Rifle! (slot 4)"
	Weapon.SelectionOrder 4000
	Tag "K&S R90-PCW"
	AttackSound "Weapons/Rifle/Fire"
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NOALERT
	+Weapon.NOAUTOFIRE
	States
	{
	Spawn:
		RIFL A -1 Nodelay A_SetAngle(random(0,359))
		Stop
	Select:
	Ready:
		RIFR T 1 Offset(200,50) A_PlaySound("Weapons/Select2")
		RIFR U 1 Offset(175,49)
		RIFR V 1 Offset(140,47)
		RIFR W 1 Offset(110,45)
		RIFR X 1 Offset(65,43)
		RIFR Y 1 Offset(50,41)
		RIFR Z 1 Offset(40,39)
		RFR2 A 1 Offset(32,38)
		RFR2 B 1 Offset(28,37)
		RFR2 C 1 Offset(20,36)
		RFR2 D 1 Offset(10,35)
	Ready2:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "ReadyZoom")
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom")
		Loop
	ReadyZoom:
		RI2G A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom")
		Goto Ready2
	Deselect:
		RI2G A 0 A_JumpIfInventory("DNRifleSighting", 1, "DeselectZoom")
		RIFG A 1 Offset(15,40)
		RIFG A 1 Offset(30,55)
		RIFG A 1 Offset(45,75)
		RIFG A 1 Offset(60,100)
		RIFG A 1 Offset(75,130)
		TNT1 A 10
		TNT1 A 0 A_Lower
		Wait
	DeselectZoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35)
		RIFG A 1 Offset(0,32)
		Goto Deselect
	Fire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "FireZoom")
		TNT1 A 0 A_JumpIfInventory("DNRifleAmmo",1,1)
		Goto Dryfire
		RIFF A 1 BRIGHT {
			A_FireBullets(2, 2, -1, 4, "BulletPuff");
			A_SpawnItemEx("RifleCasing", 12,-20,32, 8,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION);
			A_FireCustomMissile("GunSmokeSpawner",0,0,0,8);
			A_AlertMonsters;
		}
		RIFF B 1 A_SetPitch(pitch - 0.5)
		RIFF C 1 A_SetPitch(pitch + 0.25)
		RIFG A 0 A_Refire
		Goto Ready2
	FireZoom:
		TNT1 A 0 A_JumpIfInventory("DNRifleAmmo",1,1)
		Goto DryfireZoom
		RI2F A 1 BRIGHT A_FireBullets(0, 0, 1, 6, "BulletPuff")
		RIFF A 0 A_SpawnItemEx("RifleCasing", 12,-20,32, 8,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		PISF A 0 A_FireCustomMissile("GunSmokeSpawner",0,0,0,8)
		RI2F A 0 A_AlertMonsters
		RI2F B 0 A_SetAngle(angle + frandom(-1.0, 1.0))
		RI2F B 1 A_SetPitch(pitch - 1.0)
		RI2G A 1 A_SetPitch(pitch - 0.5)
		RI2G A 1 A_SetPitch(pitch + 0.5)
		RI2G A 3
		RI2G A 0 A_Refire
		Goto Ready2
	Zoom:
		TNT1 A 0 A_JumpIfInventory("PressingZoom",1,"AltHold")
		TNT1 A 0 A_GiveInventory("PressingZoom",1)
	Altfire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "AltfireUnzoom")
	AltfireZoom:
		RIFZ A 1 Offset(0,35) A_ZoomFactor(2.0)
		RIFZ B 1 Offset(0,32) A_GiveInventory("DNRifleSighting", 1)
		RIFZ C 1 A_SetCrosshair(8)
		RIFZ D 1 Offset(0,35)
		RI2G A 1 Offset(0,33)
		Goto Ready2
	AltfireUnzoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35)
		RIFG A 1 Offset(0,32)
		Goto Ready2
	Althold:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "AltholdZoomed")
		RIFG A 1 A_WeaponReady(WRF_NOFIRE)
		RIFG A 0 A_Refire
		Goto Ready2
	AltholdZoomed:
		RI2G A 1 A_WeaponReady(WRF_NOFIRE)
		RI2G A 0 A_Refire
		Goto Ready2
	DryfireZoom:
		RI2F B 1 A_PlaySound("Weapons/Dryfire", 5)
		RI2G A 5
	ReloadZoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35) A_TakeInventory("PressingZoom", 999)
		RIFG A 1 Offset(0,32)
		Goto Reload
	Dryfire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "DryfireZoom")
		RIFF B 1 A_PlaySound("Weapons/Dryfire", 5)
		RIFF C 1
		RIFG A 1
	Reload:
		RIFG A 0 A_ClearRefire
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "ReloadZoom")
		RIFG A 0 A_JumpIfInventory("DNRifleAmmo", 0, 2)
		RIFG A 0 A_JumpIfInventory("DNRifleAmmoPickup", 1, "ProperReload")
		RIFG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
	ProperReload:
		RIFR ABCDEF 1
		RIFR G 1 A_PlaySound("Weapons/Rifle/Out", 6)
		RIFR HIJKLMN 1
		TNT1 A 0 A_TakeInventory("DNRifleAmmoPickup", 1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("DNRifleAmmo", 99)
		RIFR O 1 A_PlaySound("Weapons/Rifle/In", 5)
		RIFR PQRSTUVWXY 1
		RIFR Z 1 A_PlaySound("Weapons/Rifle/Click", 6)
		RFR2 ABCD 1
		Goto Ready2
	}
}

ACTOR DNRifleSighting : Inventory{}

ACTOR DNRifleAmmo : Ammo
{
	Inventory.Icon "RIFLA0"
	Inventory.MaxAmount 30
	+IGNORESKILL
}

ACTOR DNRifleAmmoPickup : Ammo replaces ClipBox
{
	Inventory.Icon "RIFLB0"
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 20
	Inventory.PickupMessage "Picked up an assault rifle magazine."
	Inventory.PickupSound "Weapons/Rifle/In"
	Tag "6.8x40mm K&S"
	States
	{
	Spawn:
		RIFL B -1 Nodelay A_SetAngle(random(0,359))
		Stop
	}
}
