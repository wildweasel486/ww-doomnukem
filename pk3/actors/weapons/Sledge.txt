ACTOR Sledge : DNWeapon replaces Fist
{
	Weapon.Kickback 300
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 9999
	Obituary "%o was hammered in by %k"
	Tag "SLEDGE HAMMER"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	Decal SledgeDent
	States
	{
	Select:
	Ready:
		TNT1 A 5
		SLDG A 1 Offset(120,79)
		SLDG A 1 Offset(70,69)
		SLDG A 1 Offset(40,60)
		SLDG A 1 Offset(20,52)
		SLDG A 1 Offset(12,45)
		SLDG A 1 Offset(7,39)
		SLDG A 1 Offset(3,35)
	Ready2:
		SLDG A 1 A_WeaponReady
		Loop
	Deselect:
		SLDG A 1 Offset(3,35)
		SLDG A 1 Offset(7,39)
		SLDG A 1 Offset(12,45)
		SLDG A 1 Offset(20,52)
		SLDG A 1 Offset(40,60)
		SLDG A 1 Offset(70,69)
		SLDG A 1 Offset(120,79)
		TNT1 A 10
		TNT1 A 0 A_Lower
		Wait
	Fire:
		SLDG A 1 Offset(4,34)
		SLDG A 1 Offset(9,36)
		SLDG A 1 Offset(15,40)
		SLDG A 1 Offset(22,48)
		SLDG A 1 Offset(30,54)
		SLDG A 1 Offset(39,65)
		SLDG A 1 Offset(45,72)
	Hold:
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "PowerHold")
		TNT1 A 8 Offset(1,32) A_PlaySound("Weapons/Sledge/Swing", 5)
		SLDS A 1 A_SetPitch(pitch + 0.3)
		SLDS A 1 Offset(1,42) A_SetPitch(pitch + 0.6)
		SLDS B 1 Offset(1,46) A_CustomPunch(40, 1, 0, "SledgePuff", 96)
		SLDS C 1 Offset(1,65) A_SetPitch(pitch + 1.2)
		SLDS D 1 Offset(5,64) A_SetPitch(pitch + 1.8)
		SLDS D 1 Offset(5,80) A_SetPitch(pitch - 2.1)
		TNT1 A 8 A_SetPitch(pitch - 1.0)
		TNT1 A 1 A_Refire
		SLDG A 1 Offset(45,72)
		SLDG A 1 Offset(39,65)
		SLDG A 1 Offset(30,54)
		SLDG A 1 Offset(22,48)
		SLDG A 1 Offset(15,40)
		SLDG A 1 Offset(9,36)
		SLDG A 1 Offset(4,34)
		Goto Ready2
	PowerHold:
		TNT1 A 8 Offset(1,32) A_PlaySound("Weapons/ESledge/Swing", 5)
		SLDE A 1 A_SetPitch(pitch + 0.3)
		SLDE A 1 Offset(1,42) A_SetPitch(pitch + 0.6)
		SLDE B 1 Offset(1,46) A_CustomPunch(200, 1, 0, "ElectricSledgePuff", 96)
		SLDE C 1 Offset(1,65) A_SetPitch(pitch + 1.2)
		SLDE D 1 Offset(5,64) A_SetPitch(pitch + 1.8)
		SLDE D 1 Offset(5,80) A_SetPitch(pitch - 2.1)
		TNT1 A 8 A_SetPitch(pitch - 1.0)
		TNT1 A 1 A_Refire
		SLDG A 1 Offset(45,72)
		SLDG A 1 Offset(39,65)
		SLDG A 1 Offset(30,54)
		SLDG A 1 Offset(22,48)
		SLDG A 1 Offset(15,40)
		SLDG A 1 Offset(9,36)
		SLDG A 1 Offset(4,34)
		Goto Ready2
	}
}

ACTOR SledgePuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+FORCEPAIN
	RenderStyle Add
	states
	{
	Spawn:
	Melee:
		POOF AA 0 A_PlaySound("Weapons/Sledge/Hit",5)
		Goto Actualpuff
	Crash:
		POOF AA 0 A_PlaySound("Weapons/Sledge/Wall",5)
		Goto Actualpuff
	ActualPuff:
		POOF AA 0 A_Quake(3,10,0,100,"")
		POOF AAAAA 0 A_SpawnItemEx("GunSmoke", 0,0,0, random(-1,1),random(-1,1),random(-1,1), 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR ElectricSledgePuff : SledgePuff
{
	states
	{
	Spawn:
	Melee:
		POOF AA 0 A_PlaySound("Weapons/ESledge/Hit",5)
		Goto Actualpuff
	Crash:
		POOF AA 0 A_PlaySound("Weapons/ESledge/Wall",5)
		Goto Actualpuff
	ActualPuff:
		POOF AA 0 A_Quake(3,10,0,100,"")
		POOF AAAAA 0 A_SpawnItemEx("GunSmoke", 0,0,0, random(-1,1),random(-1,1),random(-1,1), 0, SXF_NOCHECKPOSITION)
		POOF A 0 A_SpawnItemEx("BlueSparkSpawner")
		Stop
	}
}