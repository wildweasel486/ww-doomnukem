ACTOR DNRocketAmmo : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon "LAUNA0"
}

ACTOR DNRocketAmmoPickup : Ammo replaces RocketAmmo
{
	Inventory.Icon "RKBXA0"
	Inventory.PickupSound "Weapons/Rocket/Pickup"
	Inventory.PickupMessage "Picked up some mini-rockets."
	Inventory.MaxAmount 10
	Ammo.BackpackMaxAmount 20
	Tag "@99 Mini-Missile"
	Scale 0.3
	States
	{
	Spawn:
		RKBX A -1 Nodelay A_SetAngle(random(0,359))
		Stop
	}
}

ACTOR DNRocketAmmoPickupBigger : DNRocketAmmoPickup replaces RocketBox
{
	Inventory.Amount 2
	Inventory.PickupMessage "Picked up a large box of mini-rockets."
	Scale .75
}

ACTOR DNRocketLauncher : DNWeapon replaces RocketLauncher
{
	Weapon.AmmoType "DNRocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "DNRocketAmmoPickup"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 1
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 5500
	Inventory.PickupSound "Weapons/Rocket/Eject"
	Inventory.PickupMessage "You got the rocket launcher! (slot 5)"
	Tag "Corbex @99 EMAW"
	+Weapon.NoAlert
	+Weapon.NoAutoFire
	+Weapon.AMMO_CHECKBOTH
	States
	{
	Spawn:
		LAUN A -1 Nodelay A_SetAngle(random(0,359))
		Stop
	Deselect:
		RCKG A 1 Offset(1,35)
		RCKG A 1 Offset(2,40)
		RCKG A 1 Offset(3,45)
		RCKG A 1 Offset(4,50)
		RCKG A 1 Offset(5,55)
		RCKG A 1 Offset(6,60)
		RCKG A 1 Offset(7,65)
		RCKG A 1 Offset(8,70)
		RCKG A 1 Offset(9,75)
		RCKG A 1 Offset(10,80)
		RCKG A 1 Offset(11,85)
		RCKG A 1 Offset(12,90)
		RCKG A 1 Offset(13,95)
		RCKG A 1 Offset(14,100)
		RCKG A 1 Offset(15,105)
		RCKG A 1 Offset(16,110)
		TNT1 A 0 A_Lower
		Wait
	Select:
	Ready:
		RCKG A 1 Offset(190,110) A_PlaySound("Weapons/Rocket/Slap",6)
		RCKG A 1 Offset(170,105)
		RCKG A 1 Offset(145,100)
		RCKG A 1 Offset(130,95)
		RCKG A 1 Offset(115,90)
		RCKG A 1 Offset(100,85)
		RCKG A 1 Offset(85,80)
		RCKG A 1 Offset(75,75)
		RCKG A 1 Offset(65,70)
		RCKG A 1 Offset(55,65)
		RCKG A 1 Offset(45,60)
		RCKG A 1 Offset(35,55)
		RCKG A 1 Offset(30,50)
		RCKG A 1 Offset(25,45)
		RCKG A 1 Offset(20,40)
		RCKG A 1 Offset(15,35)
		RCKG A 1 Offset(10,34)
		RCKG A 1 Offset(5,33)
	Ready2:
		RCKG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("DNRocketAmmo",1,1)
		Goto Dryfire
		RCKF A 0 A_PlaySound("Weapons/Rocket/Fire")
		RCKF A 1 BRIGHT Offset(0,38) A_FireCustomMissile("NebRocket")
		RCKF B 1 BRIGHT Offset(0,43) A_SetPitch(pitch - 0.3)
		RCKF C 1 BRIGHT Offset(0,46) A_SetPitch(pitch - 0.6)
		RCKG A 1 Offset(0,48) A_SetPitch(pitch + 0.3)
		RCKG A 1 Offset(0,47) A_SetPitch(pitch + 0.6)
		RCKG A 1 Offset(0,45) A_AlertMonsters
		RCKG A 1 Offset(0,42) A_Refire
		RCKG A 1 Offset(0,38) A_Refire
		RCKG A 1 Offset(0,36) A_Refire
		RCKG A 1 Offset(0,34) A_Refire
		RCKG A 1 Offset(0,33) A_Refire
		Goto Ready2
	Dryfire:
		RCKG A 1 Offset(0,35) A_PlaySound("Weapons/Dryfire", 5)
		RCKG A 1 Offset(0,39)
		RCKG A 1 Offset(0,41)
		RCKG A 1 Offset(0,37)
		RCKG A 1 Offset(0,34)
	Reload:
		RCKG A 0 A_JumpIfInventory("DNRocketAmmo", 8, 2)
		RCKG A 0 A_JumpIfInventory("DNRocketAmmoPickup", 1, 2)
		RCKG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		RCKG B 1 Offset(2,34)
		RCKG B 1 Offset(4,36)
		RCKG C 1 Offset(6,38)
		RCKG C 1 Offset(7,41)
		RCKG D 1 Offset(8,43)
		RCKG D 1 Offset(9,44)
		RCKG E 1 Offset(12,45) A_PlaySound("Weapons/Rocket/Eject",6)
		RCKG E 1 Offset(14,46)
		RCKG F 1 Offset(15,47)
		RCKG F 1 Offset(16,48)
		RCKG G 1 Offset(18,49) A_Jump(128,2)
		RCKG G 1 Offset(21,50)
		RCKG H 1 Offset(23,49)
		RCKG H 1 Offset(24,47)
		RCKG H 1 Offset(23,46)
		RCKG H 1 Offset(22,45) A_PlaySound("Weapons/Rocket/Pickup", 5)
		RCKG H 1 Offset(20,44) A_Jump(128,2)
		RCKG H 1 Offset(17,43)
		RCKG I 1 Offset(15,42)
		RCKG I 1 Offset(12,41)
		TNT1 A 0 A_TakeInventory("DNRocketAmmoPickup",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("DNRocketAmmo",8)
		RCKG J 1 Offset(10,40) A_Jump(128,2)
		RCKG J 1 Offset(9,39)
		RCKG K 1 Offset(10,40)
		RCKG K 1 Offset(11,41)
		RCKG L 1 Offset(12,42)
		RCKG L 1 Offset(13,43)
		RCKG L 1 Offset(14,44)
		RCKG M 1 Offset(15,43) A_Jump(128,2)
		RCKG M 1 Offset(14,42)
		RCKG M 1 Offset(13,43)
		RCKG N 1 Offset(12,41)
		RCKG O 1 Offset(9,38) A_PlaySound("Weapons/Rocket/Slap",6)
		RCKG P 1 Offset(6,35)
		RCKG Q 2 Offset(2,33)
		RCKG A 1 Offset(1,32)
		Goto Ready2
	}
}

actor NebRocket
{
	scale 0.5
	Radius 11
	Height 8
	Speed 55
	Damage (30)
	Projectile
	+RANDOMIZE
	DeathSound "Weapons/Rocket/Explode"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	Decal Scorch
	States
	{
	Spawn:
		MNSS A 0 A_SpawnItemEx("DN_MissileTrail",-15)
		MNSS A 1 bright A_Weave(4, 4, 1.2, 1.2)
		loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/Rocket/Explode", CHAN_BODY, 2.0, 0, 0.25)
		TNT1 A 1 A_Explode(96,128)
		TNT1 A 0 A_SpawnItemEx("DN_Blastage")
		stop
	}
}

/*

PILLOW'S SHAMELESSLY BASED ON KEKSDOSE'S PARTICLE EFFECT BUILDING METHOD!

Use as you wish, as long as you credit the fellas below.

Blast code made by PillowBlaster, graphics by KeksDose and DukeNukem.
*/

ACTOR DN_MissileTrail
{
	Renderstyle Add
	+NOINTERACTION
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("DNMissile_Smoke2",0,random(5,-5),random(5,-5),frandom(-0.5,-0.25),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)
		TNT1 A 0 A_SpawnItemEx("DNMissile_Flame2",0,random(5,-5),random(5,-5),frandom(-0.5,-0.25),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)
		TNT1 A 1 A_SpawnItemEx("DNMissile_FlightFlare",0,random(5,-5),random(5,-5),frandom(-0.5,-0.25),frandom(0.1,0.05),frandom(0.1,0.05),random(135,225),SXF_CLIENTSIDE,20)
		Stop
	}
}

Actor DN_Blastage : DN_MissileTrail
{
	States
	{
	Spawn:	//That's a lot of gobshite to spawn... and often it spawns even more gobshite!
		TNT1 A 0 NODELAY A_SpawnItemEx("DNMissile_Flare")
		TNT1 A 0 A_SpawnItemEx("DNMissile_ReverseFlare")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Shockwave")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Blast")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Cloud_Spawner")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Flame_Spawner")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Ember_Spawner")
		TNT1 A 1 A_SpawnItemEx("DNMissile_Smoke_Spawner")
		Stop
	}
}

Actor DNMissile_Flare : DN_MissileTrail
{
	Scale 0.3
	Alpha 0.7
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		FLRE AB 0 A_Jump(256,"Main")
	Main:
		"####" "#" 0 A_FadeOut(frandom(0.05,0.025))
		"####" "#" 1 Bright A_SetScale(ScaleX + frandom(0.02,0.03))
		Loop
	}
}

Actor DNMissile_ReverseFlare : DNMissile_Flare
{
	Scale 1.75
	Alpha 0.6
	States
	{
	Main:
		"####" "#" 0 A_JumpIf(ScaleX > 0, 1)
		Stop
		"####" "#" 0 A_FadeOut(frandom(0.05,0.025))
		"####" "#" 1 Bright A_SetScale(ScaleX - frandom(0.05,0.075))
		Loop
	}
}

Actor DNMissile_Shockwave : DN_MissileTrail
{
	Alpha 0.99
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		SHCK AB 0 A_Jump(256,"Main")
	Main:
		"####" "#" 0 A_FadeOut(frandom(0.1,0.15))
		"####" "#" 1 Bright A_SetScale(ScaleX + frandom(0.075,0.1))
		Loop
	}
}

Actor DNMissile_Blast : DN_MissileTrail
{
	States
	{
	Spawn: //A trick I used for instance in Violator - layered explosion!
		TNT1 A 0 NODELAY A_SpawnItemEx("DNMissile_Blast1")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Blast2")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Blast3")
		TNT1 A 0 A_SpawnItemEx("DNMissile_Blast4")
		Stop
	}
}

Actor DNMissile_Blast1 : DN_MissileTrail
{
	Scale 0.05
	Alpha 0.95
	States
	{
	Spawn:
		BLST A 0 NODELAY A_FadeOut(frandom(0.05,0.1))
		BLST A 1 A_SetScale(ScaleX + frandom(0.025,0.035))
		Loop
	}
}

Actor DNMissile_Blast2 : DN_MissileTrail
{
	Scale 0.075
	Alpha 0.8
	States
	{
	Spawn:
		BLST B 0 NODELAY A_FadeOut(frandom(0.04,0.1))
		BLST B 1 A_SetScale(ScaleX + frandom(0.035,0.045))
		Loop
	}
}

Actor DNMissile_Blast3 : DN_MissileTrail
{
	Scale 0.1
	Alpha 0.9
	States
	{
	Spawn:
		BLST C 0 NODELAY A_FadeOut(frandom(0.03,0.1))
		BLST C 1 A_SetScale(ScaleX + frandom(0.05,0.06))
		Loop
	}
}

Actor DNMissile_Blast4 : DN_MissileTrail
{
	Scale 0.125
	Alpha 0.85
	States
	{
	Spawn:
		BLST D 0 NODELAY A_FadeOut(frandom(0.02,0.1))
		BLST D 1 A_SetScale(ScaleX + frandom(0.06,0.08))
		Loop
	}
}

//This will rustle your jimmies. M-M-M-M-MAXIMUM RUSTLING!

Actor DNMissile_Smoke_Spawner : DN_MissileTrail
{
	var int User_Count;
	States
	{
	Spawn:
		TNT1 A 12 NODELAY //The 12 here is the time before the smoke will kick in.
	Mainie:
		TNT1 A 0 A_SetUserVar("User_Count", User_Count + 1)
		TNT1 A 1 A_SpawnItemEx("DNMissile_Smoke",random(15,-15),random(15,-15),random(15,-15),frandom(0.01,0.05),frandom(0.01,0.05),frandom(0.2,0.05),random(0,359),SXF_CLIENTSIDE,40)
		TNT1 A 0 A_JumpIf(User_Count < 18, "Mainie")
		Stop
	}
}

Actor DNMissile_Smoke : DN_MissileTrail
{
	Scale 0.0
	Alpha 0.0
	Renderstyle Translucent
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		DYMI ABCD 0 A_Jump(256,"Scaling")
	Scaling: //Why so derplicated? Because smoke is one hell of an ass to make look good, when mixed with rest of stuff.
		"####" "#" 0 A_SetScale(frandom(0.05,0.075))
	FadingIn:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeIn(frandom(0.02,0.04))
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.01,0.015))
		"####" "#" 1 A_JumpIf(Alpha>=0.5,"Main")
		Loop
	Main:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeOut(frandom(0.02,0.04))
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.01,0.015))
		Loop
	}
}

Actor DNMissile_Cloud_Spawner : DNMissile_Smoke_Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetUserVar("User_Count", User_Count + 1)
		TNT1 A 1 A_SpawnItemEx("DNMissile_Cloud",random(15,-15),random(15,-15),random(15,-15),frandom(0.25,0.5),frandom(0.25,0.5),frandom(0.25,0.5),random(0,359),SXF_CLIENTSIDE,40)
		TNT1 A 0 A_JumpIf(User_Count < 15, "Spawn")
		Stop
	}
}

Actor DNMissile_Cloud : DN_MissileTrail
{
	Alpha 0.99
	Scale 0.15
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12)
		CLOD ABCDEFGHIJKL 0 A_Jump(256,"Main")
	Main:
		"####" "#" 0 A_FadeOut(frandom(0.1,0.0))
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.01,0.02))
		"####" "#" random(1,2) Bright A_FadeOut(frandom(0.05,0.075))
		Goto Main+1
	}
}

Actor DNMissile_Flame_Spawner : DNMissile_Smoke_Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetUserVar("User_Count", User_Count + 1)
		TNT1 A 1 A_SpawnItemEx("DNMissile_Flame",random(15,-15),random(15,-15),random(15,-15),frandom(0.25,0.5),frandom(0.25,0.5),frandom(0.25,0.5),random(0,359),SXF_CLIENTSIDE,40)
		TNT1 A 0 A_JumpIf(User_Count < 15, "Spawn")
		Stop
	}
}

Actor DNMissile_Flame : DNMissile_Cloud
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12)
		FLAM ABCDEFGHIJKL 0 A_Jump(256,"Main")
		Stop
	}
}

Actor DNMissile_Ember_Spawner : DNMissile_Smoke_Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetUserVar("User_Count", User_Count + 1)
		TNT1 A 1 A_SpawnItemEx("DNMissile_Ember",random(10,-10),random(10,-10),random(10,-10),frandom(1.0,1.5),frandom(1.0,1.5),frandom(1.0,1.5),random(0,359),SXF_CLIENTSIDE,40)
		TNT1 A 0 A_JumpIf(User_Count < 20, "Spawn")
		Stop
	}
}

Actor DNMissile_Ember : DN_MissileTrail
{
	Alpha 0.99
	Scale 0.0
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(ScaleX + frandom(0.02,0.03))
		TNT1 A 0 A_FadeOut(frandom(0.0,0.05))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12)
		EMBR ABCDEFGHIJKL 0 A_Jump(256,"Main")
	Main:
		"####" "#" 4 Bright A_Jump(192,"NEIN")
	NEIN:
		"####" "#" 0 A_ChangeVelocity(0,0,frandom(-0.1,-0.05),CVF_RELATIVE)
		"####" "#" 1 Bright A_FadeOut(frandom(0.04,0.025))
		Loop
	}
}

//Trail stuff

Actor DNMissile_FlightFlare : DNMissile_Flare
{
	Scale 0.1
	Alpha 0.99
	States
	{
	Main:
		"####" "#" 1 Bright A_SetScale(ScaleX-random(0.001,0.003))
		"####" "#" 0 A_FadeOut(frandom(0.2,0.4))
		"####" "#" 0 A_JumpIf(ScaleX > 0, "Main")
		Stop
	}
}

Actor DNMissile_Flame2 : DNMissile_Flame
{
	Scale 0.08
	States
	{
	Main:
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.005,0.003))
		"####" "#" 1 Bright A_FadeOut(frandom(0.09,0.08))
		Loop
	}
}

Actor DNMissile_Smoke2 : DNMissile_Smoke
{
	States
	{
	Scaling:
		"####" "#" 0 A_SetScale(frandom(0.03,0.035))
	FadingIn:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeIn(frandom(0.06,0.04))
		"####" "#" 0 A_SetScale(ScaleX + frandom(0.01,0.015))
		"####" "#" 1 A_JumpIf(Alpha>=0.4,"Main")
		Loop
	Main:
		"####" "#" 0 A_ChangeVelocity(random(-0.02,0.02),random(-0.02,0.02),random(0.0,0.05),CVF_RELATIVE)
		"####" "#" 0 A_FadeOut(frandom(0.03,0.04))
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.005,0.003))
		Loop
	}
}